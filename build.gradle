import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.ajoberstar.git-publish' version '0.3.3'
    id 'com.github.onslip.gradle-one-jar' version '1.0.5'
    // need to be in the root project as workaround for https://github.com/michel-kraemer/gradle-download-task/issues/48
    id 'de.undercouch.download' version '3.1.2'
}

defaultTasks 'build' // for BuildHive

configure(subprojects - project(':job-dsl-api-viewer')) {
    group = 'org.jenkins-ci.plugins'

    apply plugin: 'groovy'
    apply plugin: 'codenarc'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        implementation platform("org.jenkins-ci.plugins:plugin:${jenkinsPluginBomVersion}")
        compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
        testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
        testCompile 'junit:junit'
        testCompile 'cglib:cglib-nodep:3.2.5' // used by Spock
        testCompile 'org.objenesis:objenesis:2.5.1' // used by Spock
    }

    codenarc {
        toolVersion = '1.2'
        configFile = file('../config/codenarc/rules.groovy')
    }

    codenarcTest {
        configFile = file('../config/codenarc/rules-test.groovy')
    }
}

project(':job-dsl-core') {
    repositories {
        maven {
            name 'artifactory'
            url 'https://vonagecc.jfrog.io/artifactory/maven'
            credentials {
                username System.getenv('ARTIFACTORY_USER')
                password System.getenv('ARTIFACTORY_SECRET')
            }
        }
    }

    apply plugin: 'application'
    apply plugin: 'gradle-one-jar'
    apply plugin: 'maven-publish'

    sourceSets {
        ast {
            groovy {
                srcDir 'src/ast/groovy'
            }
            resources {
                srcDir 'src/ast/resources'
            }
        }
        examples {
            groovy {
                srcDirs 'src/main/docs/examples'
            }
        }
        main.compileClasspath += sourceSets.ast.output
        test.compileClasspath += sourceSets.ast.output
    }

    description = 'Generates Jenkins jobs via a DSL'
    mainClassName = 'javaposse.jobdsl.Run'

    ext {
        apiDocData = "${project.buildDir}/dsl.json"
    }

    dependencies {
        astCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
        compile 'xmlunit:xmlunit:1.4' // for runtime use, not just for testing
        compile('org.jvnet.hudson:xstream:1.4.7-jenkins-1') {
            exclude group: 'xmlpull'
            exclude group: 'xpp3'
        }
    }

    codenarcExamples {
        configFile = file('../config/codenarc/rules-examples.groovy')
    }

    jar {
        manifest {
            attributes 'Main-Class': project.mainClassName
            attributes 'Implementation-Version': version
            attributes 'Jenkins-ClassFilter-Whitelisted': 'true'
        }
    }

    task jarSources(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task jarJavadocs(type: Jar, dependsOn: 'javadoc') {
        from project.javadoc.destinationDir
        classifier = 'javadoc'
    }

    task jarGroovydocs(type: Jar, dependsOn: 'groovydoc') {
        from project.groovydoc.destinationDir
        classifier = 'groovydoc'
    }

    task oneJar(type: OneJar) {
        mainClass = project.mainClassName
    }

    task generateApiDoc(type: JavaExec, dependsOn: 'classes') {
        main = 'javaposse.jobdsl.dsl.doc.ApiDocGenerator'
        classpath = sourceSets.main.runtimeClasspath
        args = [version, apiDocData]

        inputs.dir "${project.projectDir}/src/main/docs"
        outputs.file apiDocData
    }

    jar {
        from(tasks.generateApiDoc) {
            into 'javaposse/jobdsl/dsl'
        }
    }

    run {
        if (project.hasProperty('args')) {
            args project.args.split('\\s+')
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact jarJavadocs
                artifact jarGroovydocs
                artifact jarSources
                artifact oneJar
                artifact source: apiDocData, extension: 'json', classifier: 'apidoc' builtBy(project.tasks.generateApiDoc)
            }
        }

        repositories {
            maven {
                name 'artifactory'
                url 'https://vonagecc.jfrog.io/artifactory/maven-jenkins-plugins'
                credentials {
                    username System.getenv('ARTIFACTORY_USER')
                    password System.getenv('ARTIFACTORY_SECRET')
                }
            }
        }
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}
